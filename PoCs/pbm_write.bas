1 REM *****************************
2 REM Procedure to store content of
3 REM graphics memory in GR.8+16
4 REM into standard PBM file.
5 REM *****************************
6 REM 
7 REM
8 REM
9 REM
10 DIM FILENAME$(20)
20 FILENAME$="H:TEST.PBM"
30 GRAPHICS 8+16
40 COLOR 1
50 PLOT 0,0:DRAWTO 319,191
55 PLOT 319,96:DRAWTO 319,191
56 PLOT 223,191:DRAWTO 319,191
60 PLOT 319,96:DRAWTO 319,191
70 PLOT 223,191:DRAWTO 319,191
80 CIRCLE 20,20,10
90 EXEC WRITE_PBM
999 STOP 
10000 ------------------------------
10010 REM STORE VIDEO RAM
10020 REM IN GRAPHICS 8 INTO
10030 REM PBM (PORTABLE BITMAP) FILE
10040 REM FILENAME IS TO BE PROVIDED
10050 REM VIA FILENAME$ VARIABLE
10060 ------------------------------
10070 PROC WRITE_PBM
10075   OPEN #1,8,0,FILENAME$
10079   REM PBM HEADER: BINARY FORMAT
10080   PRINT #1;"P4";
10085   PUT #1,13
10089   REM BITMAP RESOLUTION
10090   PRINT #1;"320 192";
10095   PUT #1,13
10130   SCREEN_BEGIN=DPEEK(88)
10135   SCREEN_END=SCREEN_BEGIN+7680-1
10139   REM WRITE VIDEO RAM TO FILE
10140   FOR I=SCREEN_BEGIN TO SCREEN_END
10145     PUT #1,PEEK(I)
10150   NEXT I
10160   CLOSE #1
10200 ENDPROC 

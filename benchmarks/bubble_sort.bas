1 REM *****************************
2 REM Algorismus bubble sort
3 REM
4 REM Implementace pro Atari BASIC
5 REM
6 REM Upraveno do podoby benchmarku
7 REM *****************************
8 REM
9 REM
10 REM VYMAZANI CASOVACE
11 POKE 18,0
12 POKE 19,0
13 POKE 20,0
20 REM VLASTNI BENCHMARK
21 MAX=50
22 DIM A(MAX)
23 FOR I=0 TO MAX
24 A(I)=INT(100*RND(0))
25 NEXT I
26 GOSUB 100:REM TISK OBSAHU POLE
27 FOR I=MAX-1 TO 0 STEP -1
28 PRINT ".";
29 FOR J=0 TO I
30 IF A(J)<A(J+1) THEN X=A(J):A(J)=A(J+1):A(J+1)=X
31 NEXT J
32 NEXT I
33 PRINT ""
34 PRINT "SORTED:"
35 GOSUB 100:REM TISK OBSAHU POLE
40 REM PRECTENI CASOVACE
41 REM (PLATNE PRO PAL)
42 T=((PEEK(18)*65536+PEEK(19)*256+PEEK(20))/50)
50 PRINT "FINISHED IN ";T;" SECONDS"
99 END 
100 REM TISK OBSAHU POLE
101 FOR I=0 TO MAX
102 PRINT I,A(I)
103 NEXT I
104 RETURN 

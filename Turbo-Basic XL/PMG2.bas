1 REM PLAYER-MISSILE GRAPHICS
2 REM UPRAVA PRO TURBO-BASIC XL
3 REM 
4 REM ----------------------------
5 REM 
10 PMBASE=54279
20 RAMTOP=106
30 SDMCTL=559
40 GRACTL=53277
50 HPOSP0=53248
55 PCOLR0=704
60 Y=48
70 EXEC INIT_PGM
80 EXEC ERASE_P0
90 EXEC INIT_P0_BITMAP
999 END 
1000 ------------------------------
1001 REM 
1002 REM ERASE PLAYER 0
1003 REM 
1004 PROC ERASE_P0
1005   FOR I=MYPMBASE+512 TO MYPMBASE+640
1006     POKE I,0
1007   NEXT I
1008 ENDPROC 
1100 ------------------------------
1101 REM 
1102 REM INITIALIZE PLAYER 0 BITMAP
1103 REM 
1104 PROC INIT_P0_BITMAP
1105   FOR I=MYPMBASE+512+Y TO MYPMBASE+512+Y+7
1106     READ A
1107     POKE I,A
1108   NEXT I
1109   DATA 24,60,126,219,255,36,90,165
1110 ENDPROC 
1200 ------------------------------
1201 REM 
1202 REM INITIALIZE PMG
1203 REM 
1204 PROC INIT_PGM
1205   POKE PCOLR0,88
1206   A=PEEK(RAMTOP)-8
1207   POKE PMBASE,A
1208   MYPMBASE=256*A
1209   POKE SDMCTL,46
1210   POKE GRACTL,3
1211   POKE HPOSP0,128
1212 ENDPROC 

